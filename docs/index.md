---
page_title: "Provider: Hetzner Dedicated Servers"
description: |-
  The Hetzner Robot (hrobot) provider is used to interact with the resources supported by Hetzner Dedicated Servers.
---

# Hetzner Dedicated Servers Provider

The Hetzner Robot (hrobot) provider is used to interact with [Hetzner Dedicated Servers](https://www.hetzner.com/dedicated-rootserver/) or [Hetzner Auction Servers](https://www.hetzner.com/sb/).

You can both buy and configure the bare metal servers, firewalls, reverse dns, failover IPs and vswitches.

It uses the [Hetzner Robot Webservice API](https://robot.hetzner.com/doc/webservice/en.html).

Use the navigation to the left to read about the available resources.

Or follow complete example from below:

## Example Usage

```terraform
terraform {
  required_providers {
    hrobot = {
      source = "midwork-finds-jobs/hrobot"
    }
  }
}

# Credentials:Â https://robot.hetzner.com/preferences/index
# NOTE: To purchase servers you need to opt-in:
# Ordering -> ordering over the webservice -> Agree

provider "hrobot" {
  # Use $HROBOT_USERNAME and $HROBOT_PASSWORD env

  # OR:

  # username = "#ws+XXXXXXX"
  # password = "YYYYYYYYYYY"
}
resource "hrobot_ssh_key" "example" {
  name       = "example-key"
  public_key = file("~/.ssh/id_ed25519.pub")
}

resource "hrobot_server" "auction" {
  server_type = "auction"
  server_id   = 12345789 # <-- Replace this with the auction server you want
  server_name = "my-auction-server"
  image       = "Rescue system"

  public_net {
    ipv4_enabled = true
  }

  authorized_keys = [hrobot_ssh_key.example.fingerprint]
}

variable "my_ip_address" {
  type        = string
  description = "Allowed IP for SSH ports"
  default     = "127.0.0.1/32"
}

resource "hrobot_firewall_template" "ssh_access" {
  name = "Allow SSH"

  input_rules = [
    {
      name             = "Allow SSH from my IP"
      ip_version       = "ipv4"
      action           = "accept"
      protocol         = "tcp"
      destination_port = "22"
      source_ips       = [var.my_ip_address]
    },
    {
      name             = "Allow establishing TCP"
      ip_version       = "ipv4"
      action           = "accept"
      protocol         = "tcp"
      destination_port = "32768-65535"
      tcp_flags        = "ack"
    },
    {
      name       = "Drop all other input"
      ip_version = "ipv4"
      action     = "discard"
    }
  ]

  output_rules = [
    {
      name       = "Allow all output"
      ip_version = "ipv4"
      action     = "accept"
    }
  ]
}

resource "hrobot_firewall" "server_firewall" {
  server_id   = hrobot_server.auction.server_id
  template_id = hrobot_firewall_template.ssh_access.id
}
```

<!-- schema generated by tfplugindocs -->
## Schema

### Optional

- `password` (String, Sensitive) Hetzner Robot API password. Can also be set via HROBOT_PASSWORD environment variable.
- `username` (String) Hetzner Robot API username. Can also be set via HROBOT_USERNAME environment variable.